import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'

    id "org.openapi.generator" version "7.1.0"
    id 'project-report'
}

group = 'com.github.dhslrl321'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-ui
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'


    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // openapi generator 적용을 위한 정보
    implementation('org.openapitools:openapi-generator-gradle-plugin:7.1.0') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    // generation 에 필요한 의존성 정보들
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.19'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

}

tasks.named('test') {
    useJUnitPlatform()
}

// oas dir


def baseDir = "$projectDir/src/api-spec"

def apiSpecDir = "$baseDir/spec"
def docsDir = "$baseDir/docs"
def generatedDir = "$baseDir/.generated"

def configPath = "$apiSpecDir/config.json"
def specPath = "$apiSpecDir/api-spec.yaml"

def docsResourceDir = "$generatedDir/src/main/resources/docs"


// validation
openApiValidate { // task 1. validation
    inputSpec.set(specPath)
    recommend.set(true)
}



openApiGenerate { // task 2. generate
    generatorName.set("spring") // spring codegen 을 명시
    inputSpec.set(specPath)
    outputDir.set(generatedDir)
    configFile.set(configPath) // configOptions 를 통해 gradle file 에서 직접 지정할 수도 있긴 함
}

tasks.named("openApiGenerate") {
    it.dependsOn("openApiValidate")
    it.dependsOn("openApiGenernalHtmlGenerate")
    doFirst {
        def javaSourceDir = "$generatedDir/src/main/java";
    }
}

// swagger ui routing
task openApiGenernalHtmlGenerate(type: GenerateTask) {
    inputSpec.set(specPath)
    generatorName.set("html2")
    outputDir.set("$docsResourceDir/html")
}

tasks.compileJava.dependsOn tasks.openApiValidate, tasks.openApiGenerate, tasks.openApiGenernalHtmlGenerate

sourceSets.main.java.srcDir "${openApiGenerate.outputDir.get()}/src/main/java"
sourceSets.main.resources.srcDir "${openApiGenerate.outputDir.get()}/src/main/resources"

/*// gradle clean 시 spec file 제거
tasks.register('cleanOasGenerated', Delete) {
    delete project.file(generatedDir).absolutePath
}

clean.dependsOn cleanOasGenerated*/

